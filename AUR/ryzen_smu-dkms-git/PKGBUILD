_pkgbase=ryzen_smu
pkgname=ryzen_smu-dkms-git
pkgver=179.b0e87fb
pkgrel=1
pkgdesc="A Linux kernel driver that exposes access to the SMU (System Management Unit) for certain AMD Ryzen Processors"
arch=('x86_64')
url="https://github.com/kylon/ryzen_smu"
license=('GPL2')
depends=('dkms')
makedepends=('git')
provides=('ryzen_smu' 'ryzen_smu-dkms')
conflicts=('ryzen_smu' 'ryzen_smu-dkms')
install=$_pkgbase.install

source=("${_pkgbase}::git+$url.git"
        "dkms.conf"
        "$_pkgbase.conf")

sha256sums=('SKIP'
            'SKIP'
            'a48f5c846ca5fd8f185f1317c8001c97f59ac432392d53b581802c7761b9360f')

pkgver() {
  cd "$srcdir/$_pkgbase"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_pkgbase"

  rm -rf scripts/ pics/ userspace/ README.md
}

build() {
  cd "$srcdir/$_pkgbase"
  make modules
}

package() {
  cd "$srcdir/$_pkgbase"

  install -d "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
  cp ${srcdir}/${_pkgbase}/{LICENSE,Makefile,dkms.conf,drv.c,smu.c,smu.h} "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
  mkdir -p "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/lib
  cp ${srcdir}/${_pkgbase}/{lib/smu_common.c,lib/smu_common.h} "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/lib

  install -Dm644 ${srcdir}/dkms.conf "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  sed -e "s/@_PKGBASE@/${_pkgbase}/" \
    -e "s/@PKGVER@/${pkgver}/" \
    -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  install -Dm644 ${srcdir}/${_pkgbase}.conf "${pkgdir}"/usr/lib/depmod.d/${_pkgbase}.conf
}
